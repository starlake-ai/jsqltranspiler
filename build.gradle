import se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask
import java.time.Instant

plugins {
    id 'java'
    id 'application'
    id "com.vanniktech.maven.publish" version "latest.release"
    id 'maven-publish'
    id 'signing'

    id 'pmd'
    id 'checkstyle'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version "latest.release"
    id "com.github.spotbugs" version "latest.release"
    id "com.diffplug.spotless" version "latest.release"

    id "de.undercouch.download" version "latest.release"
    id 'org.hidetake.ssh' version "latest.release"
    id "com.github.hierynomus.license" version "latest.release"
    id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "latest.release"
    id 'biz.aQute.bnd.builder' version "latest.release"
}

def getVersion = { boolean considerSnapshot ->
    Integer major = 0
    Integer minor = 0
    Integer patch = null
    Integer build = null
    String commit = null
    String snapshot = ""

    def versionStr = providers.exec {
        commandLine "git", "--no-pager", "-C", project.projectDir,  "describe", "--tags", "--always", "--dirty=-SNAPSHOT"
    }.standardOutput.asText.get().trim()

    def pattern = /(?<major>\d*)\.(?<minor>\d*)(\.(?<patch>\d*))?(-(?<build>\d*)-(?<commit>[a-zA-Z\d]*))?/
    def matcher = versionStr =~ pattern

    if (matcher.find()) {
        major = matcher.group('major') as Integer ?: 0
        minor = matcher.group('minor') as Integer ?: 0
        patch = matcher.group('patch') as Integer ?: null
        build = matcher.group('build') as Integer ?: null
        commit = matcher.group('commit') ?: null
    }

    if (versionStr.endsWith('SNAPSHOT') || build != null) {
        minor++
        if (patch != null) patch = 0
    }

    if (considerSnapshot) {
        snapshot = "-SNAPSHOT"
    }

    return "${major}.${minor}" +
            (patch != null ? ".${patch}" : "") +
            snapshot
}


// for publishing a release, call Gradle with Environment Variable RELEASE:
// RELEASE=true gradle JSQLTranspiler:publish
version = getVersion( !System.getenv("RELEASE") )

repositories {
    gradlePluginPortal()
    mavenCentral()

    // Snapshots
    if (version.endsWith('SNAPSHOT')) {
        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
        }
    }
}

configurations {
    xmlDoclet
}

dependencies {
    implementation('com.manticore-projects.jsqlformatter:jsqlparser:+'){ changing = true }

    //for JSON (de)serialization
    implementation 'org.json:json:+'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.4'

    implementation 'com.h2database:h2:+'

    testImplementation 'org.duckdb:duckdb_jdbc:1.4.3.0'
    testImplementation 'net.snowflake:snowflake-jdbc:+'

    testImplementation 'org.apache.commons:commons-compress:+'
    testImplementation 'com.opencsv:opencsv:+'

    // we do need better matchers
    testImplementation("org.assertj:assertj-core:+")

    // for the ASCII Trees
    testImplementation 'hu.webarticum:tree-printer:+'

    //calling external python
    testImplementation 'org.apache.commons:commons-exec:+'

    // executing sql bigquery
    testImplementation 'com.google.cloud:google-cloud-bigquery:+'

    // read test schema information
    testImplementation 'org.yaml:snakeyaml:+'

    testImplementation('com.manticore-projects.jsqlformatter:jsqlformatter:+'){ changing = true }

    // Java Doc in XML Format
    xmlDoclet ('com.manticore-projects.tools:xml-doclet:+'){ changing = true }
}

configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'com.manticore-projects.jsqlformatter') {
            // Check for updates every build
            resolutionStrategy.cacheChangingModulesFor 30, 'seconds'
        }
    }
}


group = 'ai.starlake.jsqltranspiler'
description = 'JSQLTranspiler'

tasks.register('generateBuildInfo') {
    outputs.dir layout.buildDirectory.file("resources/main")
    doLast {
        def outputDir =  new File( layout.buildDirectory.file("generated/sources/buildinfo/java/main").get().asFile, "ai/starlake/transpiler")
        outputDir.mkdirs()

        def gitVersionStr = providers.exec {
            commandLine "git", "--no-pager", "-C", project.projectDir,  "describe", "--tags", "--always", "--dirty=-SNAPSHOT"
        }.standardOutput.asText.get().trim()

        def gitCommitStr = providers.exec {
            commandLine "git", "--no-pager", "-C", project.projectDir,  "rev-parse", "--short", "HEAD"
        }.standardOutput.asText.get().trim()

        def buildTime = Instant.now().toString()

        def content = """\
            |package ai.starlake.transpiler;
            |
            |public final class BuildInfo {
            |  public static final String NAME = "${project.name}";
            |  public static final String VERSION = "${gitVersionStr}";
            |  public static final String GIT_COMMIT = "${gitCommitStr ?: 'unknown'}";
            |  public static final String BUILD_TIME = "${buildTime}";
            |}
            """.stripMargin()

        new File(outputDir, "BuildInfo.java").text = content
    }
}

// Make sure the file is included in the compiled sources
sourceSets {
    main {
        java {
            srcDir layout.buildDirectory.file("generated/sources/buildinfo/java/main").get().asFile
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    mustRunAfter("generateBuildInfo")
}

java {
    withSourcesJar()
    //withJavadocJar()

    sourceCompatibility = '11'
    targetCompatibility = '11'

    // needed for XML-Doclet to work (since Doclet changed again with Java 13)
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

application {
    mainClass.set("ai.starlake.transpiler.JSQLTranspiler")
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    options.addBooleanOption("Xdoclint:none", true)
}

tasks.register('xmldoc', Javadoc) {
    dependsOn(compileJava)

    source = sourceSets.main.allJava
    classpath = sourceSets.main.runtimeClasspath

    destinationDir = reporting.file("xmlDoclet")
    options.docletpath = configurations.xmlDoclet.files as List
    options.doclet = "com.manticore.tools.xmldoclet.XmlDoclet"
    title = "API $version"
    options.addBooleanOption("rst", true)
    options.addBooleanOption("withFloatingToc", true)
    options.addStringOption("basePackage", "ai.starlake.jsqltranspiler")

    copy {
        from reporting.file("xmlDoclet/javadoc.rst")
        into "${projectDir}/src/site/sphinx/"
    }
}

jar {
    manifest {
        attributes(
            "Created-By": System.properties['user.name'],
            "Main-Class": "ai.starlake.transpiler.JSQLTranspiler",
            "Automatic-Module-Name": "ai.starlake.transpiler"
        )
    }

    bundle {
        properties.empty()
        bnd(
            "Created-By": System.properties.get('user.name'),
            "Main-Class": "ai.starlake.transpiler.JSQLTranspiler",
            "Bundle-SymbolicName": "ai.starlake.transpiler",
            "Import-Package": "org.duckdb.*;resolution:=optional,org.h2.*;resolution:=optional,org.json.*;resolution:=optional,*",
            "Export-Package": "ai.starlake.transpiler.*",
            "Automatic-Module-Name": "ai.starlake.transpiler"
        )
    }

    dependsOn(generateBuildInfo)
}

test {
    useJUnitPlatform()

    // set heap size for the test JVM(s)
    minHeapSize = "1G"
    maxHeapSize = "4G"

    // Set maxParallelForks to 1 to ensure only one test process runs at a time
    maxParallelForks = 1

    // Ensure JUnit 5 executes tests sequentially
    systemProperty "junit.jupiter.execution.parallel.enabled", "false"


    jacoco {
        excludes = ['net/sf/jsqlparser/parser/CCJSqlParserTokenManager']
    }

    doFirst {
        // Download Amazon Redshift `TickitDB` example
        // Use the Gradle task in order to allow caching
        download.run {
            src 'https://docs.aws.amazon.com/redshift/latest/gsg/samples/tickitdb.zip'
            dest "build/resources/test/ai/starlake/transpiler/tickitdb.zip"
            overwrite false
            onlyIfModified true
            tempAndMove true
        }

        // Download Amazong Redshift geo-spatial example
        // public datasets that correlate location data of rental accommodations with postal codes in Berlin, Germany.
        download.run {
            src 'https://s3.amazonaws.com/redshift-downloads/spatial-data/accommodations.csv'
            dest "build/resources/test/ai/starlake/transpiler/accommodations.csv"
            overwrite false
            onlyIfModified true
            tempAndMove true
        }

        download.run {
            src 'https://s3.amazonaws.com/redshift-downloads/spatial-data/zipcode.csv'
            dest "build/resources/test/ai/starlake/transpiler/zipcode.csv"
            overwrite false
            onlyIfModified true
            tempAndMove true
        }
    }

    // Prefer Gradle property, fall back to environment variable
    environment "SNOWFLAKE_USER", project.findProperty("SNOWFLAKE_USER") ?: System.getenv("SNOWFLAKE_USER")
    environment "SNOWFLAKE_PASSWORD", project.findProperty("SNOWFLAKE_PASSWORD") ?: System.getenv("SNOWFLAKE_PASSWORD")
    environment "SNOWFLAKE_ACCOUNT", project.findProperty("SNOWFLAKE_ACCOUNT") ?: System.getenv("SNOWFLAKE_ACCOUNT")
    environment "SNOWFLAKE_WAREHOUSE", project.findProperty("SNOWFLAKE_WAREHOUSE") ?: System.getenv("SNOWFLAKE_WAREHOUSE")
    environment "SNOWFLAKE_DATABASE", project.findProperty("SNOWFLAKE_DATABASE") ?: System.getenv("SNOWFLAKE_DATABASE")
    environment "SNOWFLAKE_SCHEMA", project.findProperty("SNOWFLAKE_SCHEMA") ?: System.getenv("SNOWFLAKE_SCHEMA")

    jvmArgs '--add-opens=java.base/java.nio=org.apache.arrow.memory.core,ALL-UNNAMED'
}

license {
    excludes(["**/*.txt", "**/*.conf", "**/*.sql",])
    includes(["**/*.properties", "**/*.java", "**/*.xml"])

    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Starlake.AI'
    ext.email = 'hayssam.saleh@starlake.ai'

    strictCheck = false
    ignoreFailures = true
}

coveralls {
    jacocoReportPath  layout.buildDirectory.file('reports/jacoco/test/jacocoTestReport.xml')
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "net/sf/jsqlparser/parser/**"
            ])
        }))
    }
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco')
    }
}
jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "net/sf/jsqlparser/parser/**"
            ])
        }))
    }
    violationRules {
        rule {
            //element = 'CLASS'
            limit {
                minimum = 0.10
            }
            excludes = [
            ]
        }
        rule {
            //element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'MISSEDCOUNT'

                maximum = 2048
            }
            excludes = [
            ]
        }
    }
}

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("build/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugs {
    // fail only on P1 and without the net.sf.jsqlparser.parser.*
    excludeFilter = file("config/spotbugs/spotBugsExcludeFilter.xml")

    // do not run over the test, although we should do that eventually
    spotbugsTest.enabled = false
}

pmd {
    toolVersion = "7.6.0"

    consoleOutput = true
    sourceSets = [ sourceSets.main, sourceSets.test]

    // clear the ruleset in order to use configured rules only
    ruleSets = []

    //rulesMinimumPriority = 1
    ruleSetFiles = files("config/pmd/ruleset.xml")

    pmdMain {
        excludes = [
                "build/generated/*"
        ]
    }
}

tasks.withType(Pmd).configureEach {
    mustRunAfter("generateBuildInfo")
}

checkstyle {
    sourceSets = [sourceSets.main, sourceSets.test]
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')

}

tasks.withType(Checkstyle).configureEach {
    exclude '**/module-info.java', '**/package-info.java'

    mustRunAfter("generateBuildInfo")
}

spotless {
    // optional: limit format enforcement to just the files changed by this feature branch
    ratchetFrom 'origin/main'

    format 'misc', {
        // define the files to apply `misc` to
        target '*.rst', '*.md', '.gitignore'

        // define the steps to apply to those files
        trimTrailingWhitespace()
        leadingTabsToSpaces(4) // or spaces. Takes an integer argument if you don't like 4
        endWithNewline()
    }
    java {
        leadingTabsToSpaces(4)
        eclipse().configFile('config/formatter/eclipse-java-google-style.xml')
    }
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = false
        html.required = true
    }
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()

    coordinates(group, "jsqltranspiler", version)

    pom {
        name = 'JSQLTranspiler library'
        description = 'Rewrite Big RDBMS queries into DuckDB compatible queries'
        url = 'https://github.com/starlake-ai/jsqltranspiler'
        scm {
            url = 'https://github.com/starlake-ai/jsqltranspiler'
        }
        licenses {
            license {
                name = 'Apache License'
                url = 'https://www.apache.org/licenses/LICENSE-2.0'
            }
        }
        developers {
            developer {
                id = 'are'
                name = 'Andreas Reichel'
                email = 'andreas@manticore-projects.com'
            }
        }
    }
}


// Fix signing task dependencies
tasks.withType(AbstractPublishToMaven).configureEach {
    dependsOn(tasks.withType(Sign))
}

signing {
    required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("publish") }
}

// Resolve dynamic dependencies to concrete versions in POM
publishing {
    publications {
        configureEach {
            if (it instanceof MavenPublication) {
                // Suppress the warning about Maven-incompatible version notation
                it.suppressPomMetadataWarningsFor('runtimeElements')

                it.pom.withXml {
                    def runtimeDeps = project.configurations.runtimeClasspath
                            .resolvedConfiguration
                            .firstLevelModuleDependencies

                    def depsNode = asNode().dependencies?.dependency

                    depsNode?.each { depNode ->
                        def groupId = depNode.groupId.text()
                        def artifactId = depNode.artifactId.text()

                        // Find the resolved dependency
                        def resolved = runtimeDeps.find {
                            it.moduleGroup == groupId && it.moduleName == artifactId
                        }

                        if (resolved != null) {
                            // Replace dynamic version (+, latest.release, etc.) with resolved version
                            depNode.version[0].value = resolved.moduleVersion
                        }
                    }
                }
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 11
}

tasks.withType(Test).configureEach {
    maxParallelForks = 10
}

tasks.register('gitChangelogTask', GitChangelogTask) {
    fromRepo.set( file("$projectDir").toString() )
    file.set( new File("${projectDir}/src/site/sphinx/changelog.rst") )
    templateContent.set ("""
************************
Changelog
************************


{{#tags}}
{{#ifMatches name "^Unreleased.*"}}
Latest Changes since |JSQLTRANSPILER_VERSION|
{{/ifMatches}}
{{#ifMatches name "^(?!Unreleased).*"}}
Version {{name}}
{{/ifMatches}}
=============================================================

 {{#issues}}

  {{#commits}}
   {{#ifMatches messageTitle "^(?!Merge).*"}}
  * **{{{messageTitle}}}**
    
    {{authorName}}, {{commitDate}}
   {{/ifMatches}}
  {{/commits}}

 {{/issues}}
{{/tags}}
""")
}

remotes {
    webServer {
        host = findProperty("${project.name}.host")
        user = findProperty("${project.name}.username")
        identity = new File("${System.properties['user.home']}/.ssh/id_rsa")
    }
}

ssh.settings {
    timeoutSec = 60000
}

tasks.register('upload') {
    dependsOn(jar, gitChangelogTask, xmldoc)
    doFirst {
        if (findProperty("${project.name}.host") == null) {
            println(
                    """
                Property \"${project.name}.host\' not found.
                Please define \"${project.name}.host\" in the Gradle configuration (e. g. \$HOME/.gradle/gradle.properties.
                """
            )
        }
    }
    doLast {
        ssh.run {
            session(remotes.webServer) {
                def versionStable = getVersion(false)
                execute "mkdir -p download/${project.name}-${versionStable}"
                for (File file: fileTree(include:['*.jar'], dir:"${project.buildDir}/libs").collect()) {
                    put from: file, into: "download/${project.name}-${versionStable}"
                }
            }
        }
    }
}


